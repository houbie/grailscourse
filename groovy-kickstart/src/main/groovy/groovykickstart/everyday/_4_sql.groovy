package groovykickstart.everyday

import groovy.sql.Sql
import org.h2.jdbcx.JdbcDataSource

import java.text.SimpleDateFormat


Sql db = new Sql(new JdbcDataSource(
        URL: 'jdbc:h2:mem:test;DB_CLOSE_DELAY=-1',
        user: 'sa',
        password: ''))


db.execute '''
    DROP   TABLE Person IF EXISTS;
    CREATE TABLE Person (
      personId   INTEGER GENERATED BY DEFAULT AS IDENTITY,
      firstName   VARCHAR(64),
      lastName    VARCHAR(64),
      dateOfBirth DATE
    );
    CREATE INDEX PersonIdx ON Person (personId);
    INSERT INTO Person (firstName, lastName, dateOfBirth) VALUES ('Jane', 'Doe', '1980-03-12');
'''

def birthDate =  new SimpleDateFormat('yyy-MM-dd').parse('1980-03-12')
assert db.rows('SELECT * FROM Person') == [[PERSONID: 1, FIRSTNAME: 'Jane', LASTNAME: 'Doe', DATEOFBIRTH: birthDate]]
assert db.firstRow('SELECT * FROM Person') == [PERSONID: 1, FIRSTNAME: 'Jane', LASTNAME: 'Doe', DATEOFBIRTH: birthDate]


db.eachRow('SELECT firstName, lastName FROM Person WHERE firstName = ?', ['Jane']) { row ->
    assert row[0] ==  row.firstName // you can use positional or named references
    println "sql with parameter list: $row.firstName  $row.lastName"
}

db.eachRow('SELECT firstName, lastName FROM Person WHERE firstName = :firstName AND lastName = :lastName', [firstName: 'Jane', lastName: 'Doe']) { row ->
    println "sql with named parameters: $row.firstName  $row.lastName"
}


personSet = db.dataSet('Person')

personSet.add(
        firstName: 'Jack',
        lastName: 'Jones',
        dateOfBirth: '1970-10-11')

personSet.each {
    println it.firstName
}
